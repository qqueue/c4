fs       = require \fs
coco     = require \coco
stylus   = require \stylus
{cjsify} = require \commonjs-everywhere
esprima  = require \esprima

ls = -> "#it/#file" for file of fs.readdirSync it

flatten = -> Array::concat.apply [], it # shallow flatten

join = -> flatten @@ .join \\n
read = -> fs.readFileSync it, \utf8

compile = (it, options) ->
  try
    coco.compile do
      read it
      options
  catch
    throw new Error "Compiling #it:\n\t#{e.message}"

wrap = -> "let\n  #{read it .replace /\n/g '\n  '}"

compile-css = (cb) ->
  # ordering of files matters
  files = <[base yotsuba-colors page post postform thread youtube]>
  source = (for files => read "style/#&.styl")join \\n
  # TODO use sync API
  # TODO also use @import statements instead of ordering, if stylus will
  # compile them out.
  nib source .render cb

# stuff each file into a `let` IEFE, and then compile, which
# avoids coco's redefinition of boilerplate
compile-all = (css, paths) ->
  source = []
  for path of paths
    if fs.existsSync "src/#path/"
      # directory
      for ls "src/#path"
        compile & # detect compile errors
        source.push wrap &
    else
      # single file
      compile "src/#path.co"
      source.push wrap "src/#path.co"
  try
    coco.compile do
      source
        .join \\n
        .replace /%hakase\.css%/ css
        .replace /%board\.co\.html%/ read \templates/board.co.html
      {+bare} # IEFE is added later
  catch
    throw new Error "Compiling all:\n\t#{e.message}"

nib = -> stylus it .use require(\nib)!

outfile = \html5chan.user.js
metadata = read \metadata.txt

task \build 'build userscript' ->
  board-template = read \templates/board.co.html
  err, css <- compile-css
  try
    throw err if err

    ast = cjsify \src/html5chan.co __dirname,
      export: \html5chan
      handlers: \.co : ->
        esprima.parse coco.compile do
          # XXX there should be a better way to build in CSS into userscripts,
          # but I don't have much excuse for the .co.html thing. Someday, jade...
          it.toString!
            .replace /%hakase\.css%/ css
            .replace /%board\.co\.html%/ board-template

          {+bare} # because modules

    # TODO something with sourcemap, when fire(fox|bug) gets support
    gen = require \escodegen .generate ast

    fs.writeFileSync do
      outfile
      join do
        metadata
        "(function(){"
        '"use strict";'
        gen
        "}).call(this)"
    console.log "compiled script to #{outfile}"
  catch
    console.error e.message

# TODO re-include underscore/lo-dash/component/whatever is hip now for defining
# standard functions like this
debounce = (delay, fn) ->
  var timeout
  !->
    ctx = this
    args = arguments
    clearTimeout timeout
    timeout := setTimeout do
      !-> fn.apply ctx, args
      delay

task \watch 'watch for changes and rebuild automatically' ->
  invoke \build
  for it of <[style src templates src/features src/classes src/utils]>
    fs.watch it, interval: 1000, debounce 1000 (event, filename) ->
      console.log "#event event detected on #filename. rebuilding..."
      invoke \build

