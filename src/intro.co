console.group "html5chan"

console.timeStamp "html5chan-init"
console.time "init"
console.time "interactive"

export board = {}

board
  # boards.4chan.org/<name>/'res' or <page>/<threadid>
  [ , &name, page, &thread-id] = window.location.pathname / \/

  &is-thread   = !!&thread-id
  &is-board    = not &is-thread
  &page        = parseInt page, 10 or 0

  &url         = "//boards.4chan.org/#{&name}/"
  &threadurl   = &url + \res/
  &thread-path = "/#{&name}/res/#{&thread-id}"

  &archive = switch &name
    case <[a jp m tg u tv v vg]> # foolz
      "http://archive.foolz.us/#that/thread"
    case \lit
      "http://fuuka.warosu.org/#that/thread"
    case <[diy g sci]>
      "http://archive.installgentoo.net/#that/thread"

  &ready = false # flag similar to document.readyState

# early abort for archive redirection
if /404/.test document.title and board.archive
  if /\d+/.exec window.location.pathname
    window.location = "#{board.archive}/#{that.0}"
    return

# shortcuts for custom events
export
  onready      = !-> document.addEventListener \html5chan-loaded     it
  onupdate     = !-> document.addEventListener \html5chan-update     it
  onpostinsert = !-> document.addEventListener \html5chan-postinsert it
  onbacklink   = !-> document.addEventListener \html5chan-backlink   it

