$.fn.extend do
	immediateText: -> 
		@parent().clone().children().remove().end().text()
	
	exists: (selector) -> 
		( if selector then @find selector else this ).length > 0
	
	changeTo: (replacement, options) -> 
		@replaceWith ->
			$(replacement,options).html($(this).html())

	constrainY: ({left, top}, margin = 0) ->
		height = @height() 
		bottom = top + height
		screentop = $(window).scrollTop()
		screenbottom = screentop + $(window).height()
		
		if bottom > screenbottom
			top = screenbottom - margin - height
			
		if top < screentop
			top = screentop + margin
			
		@css do
			left: left
			top: top
			position: "absolute"
			
	beforeAndScroll: (content) ->
		before = @offset()
		@before content
		after = @offset()
		window.scrollBy after.left - before.left, after.top - before.top 
		return this
		
	removeAndScroll: (relativeTo) ->
		before = relativeTo.offset()
		@remove()
		after = relativeTo.offset()
		# unless the scrollbar is already at the bottom (which autocorrects position)
		unless window.scrollY is window.scrollMaxY
			window.scrollBy after.left - before.left, after.top - before.top
		return this

# special prefixed getters and setters
# though honestly, I think greasemonkey wraps storage
# so there's no chance of collisions anyway
sset = -> sessionStorage.setItem "html5chan-#key", val for key, val in it
sget = -> sessionStorage.getItem "html5chan-#it"
set = -> localStorage.setItem "html5chan-#key" val for key, val in it
get = -> localStorage.getItem "html5chan-#it"

create = (tag, fn) -> # helper method to avoid temporary variables with createElement
	el = document.createElement tag
	fn.call el
	el
