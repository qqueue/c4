# features helpers, specific to html5chan
# extracting out boilerplate for fewer absolute SLOC

{$$} = require \./dom

# shortcuts for custom events
export
  onprerender  = !-> document.addEventListener \html5chan-prerender  it
  onready      = !-> document.addEventListener \html5chan-ready      it
  onupdate     = !-> document.addEventListener \html5chan-update     it
  onpostinsert = !-> document.addEventListener \html5chan-postinsert it
  onbacklink   = !-> document.addEventListener \html5chan-backlink   it

# define listeners that are attached to every post. e.g.
#
# on-posts do
#   '.file': click: -> handler...
#   '.poster': mouseover: -> handler...
#
# attaches the click handler to all '.file' elements in each post, the
# mouseover handler to all '.poster' els, and so on.
export on-posts = !(listener-spec) ->
  for selector, listeners in listener-spec
    for event, listener in listeners
      let
        onready !->
          # initially, run one query
          for it.detail.el.querySelectorAll selector
            &addEventListener event, listener

          # for updates, use insert
          onpostinsert !->
            for it.detail.post.querySelectorAll selector
              &addEventListener event, listener

