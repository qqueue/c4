# features helpers, specific to c4
# extracting out boilerplate for fewer absolute SLOC

{$$} = require \./dom

# shortcuts for custom events
# XXX really annoying firefox bug hides errors, so catch and log
# https://bugzilla.mozilla.org/show_bug.cgi?id=851765
function catch-event evt
  return (handler) ->
    document.addEventListener evt, !->
      try
        handler ...
      catch
        console.error e
        throw e
export
  onprerender  = catch-event \c4-prerender
  onready      = catch-event \c4-ready
  onupdate     = catch-event \c4-update
  onpostinsert = catch-event \c4-postinsert
  onbacklink   = catch-event \c4-backlink

# define listeners that are attached to every post. e.g.
#
# on-posts do
#   '.file': click: -> handler...
#   '.poster': mouseover: -> handler...
#
# attaches the click handler to all '.file' elements in each post, the
# mouseover handler to all '.poster' els, and so on.
export on-posts = !(listener-spec) ->
  for selector, listeners in listener-spec
    for event, listener in listeners
      let
        onready !->
          # initially, run one query
          for it.detail.el.querySelectorAll selector
            &addEventListener event, listener

          # for updates, use insert
          onpostinsert !->
            for it.detail.post.querySelectorAll selector
              &addEventListener event, listener

