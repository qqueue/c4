# features helpers, specific to c4
# extracting out boilerplate for fewer absolute SLOC

{$$} = require \./dom

# shortcuts for custom events
function custom-handle evt
  return (handler) ->
    document.addEventListener evt, !->
      # since the rest of the CustomEvent object isn't that useful, use the
      # details hash for both `this` and the first argument, whatever's more
      # convenient
      handler.call it.detail, it.detail

for <[ prerender ready update postinsert backlink ]>
  exports"on#&" = custom-handle "c4-#&"

# define listeners that are attached to every post. e.g.
#
# on-posts do
#   '.file': click: -> handler...
#   '.poster': mouseover: -> handler...
#
# attaches the click handler to all '.file' elements in each post, the
# mouseover handler to all '.poster' els, and so on.
{onready, onpostinsert} = exports
export on-posts = !(listener-spec) ->
  for selector, listeners in listener-spec
    for event, listener in listeners
      let
        onready !->
          # XXX @el and @post are null in some cases
          # for now, just prevent the error message

          # initially, run one query
          if @el?
            for @el.querySelectorAll selector
              &addEventListener event, listener

          # for updates, use insert
          onpostinsert !->
            if @post?
              for @post.querySelectorAll selector
                &addEventListener event, listener

