# features helpers, specific to c4
# extracting out boilerplate for fewer absolute SLOC

{$$} = require \./dom

# shortcuts for custom events
# XXX really annoying firefox bug hides errors, so catch and log
# https://bugzilla.mozilla.org/show_bug.cgi?id=851765
function catch-event evt
  return (handler) ->
    document.addEventListener evt, !->
      try
        # since the rest of the CustomEvent object isn't that useful, use the
        # details hash for both `this` and the first argument, whatever's more
        # convenient
        handler.call it.detail, it.detail
      catch
        console.error e
        throw e

for <[ prerender ready update postinsert backlink ]>
  exports"on#&" = catch-event "c4-#&"

# define listeners that are attached to every post. e.g.
#
# on-posts do
#   '.file': click: -> handler...
#   '.poster': mouseover: -> handler...
#
# attaches the click handler to all '.file' elements in each post, the
# mouseover handler to all '.poster' els, and so on.
export on-posts = !(listener-spec) ->
  for selector, listeners in listener-spec
    for event, listener in listeners
      let
        onready !->
          # initially, run one query
          for @el.querySelectorAll selector
            &addEventListener event, listener

          # for updates, use insert
          onpostinsert !->
            for @post.querySelectorAll selector
              &addEventListener event, listener

