# relative dates

pluralize = (number, unit) ->
  "#{Math.round number} #unit#{if number >= 1.5 then \s else ''} ago"

# XXX explicit export of constants for features/relative-dates.co
export const YEAR   = 3.156e10_ms
             MONTH  = 2.62974e9_ms
             DAY    = 86_400_000ms
             HOUR   = 3_600_000ms
             MINUTE = 60_000ms
             SECOND = 1000ms

Date::relative-date = (now = Date.now!) ->
  diff = now - @getTime!
  absdiff = Math.abs diff
  # TODO use new if block
  if absdiff < MINUTE
    number = absdiff / SECOND
    unit = \second
  else if absdiff < HOUR
    number = absdiff / MINUTE
    unit = \minute
  else if absdiff < DAY
    number = absdiff / HOUR
    unit = \hour
  else if absdiff < MONTH
    number = absdiff / DAY
    unit = \day
  else if absdiff < YEAR
    number = absdiff / MONTH
    unit = \month
  else
    number = absdiff / YEAR
    unit = \year

  number = Math.round number
  unit += \s unless number is 1

  if diff > 0
    "#number #unit ago"
  else
    "in #number #unit"


