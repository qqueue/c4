# why do I need to parse the already JSON-formatted 4chan API. because it's
# dumb, that's why.
# `data` is the JSON parsed to JS objects
# `board` is the board name, e.g. 'a'
#
# Note that this parses the thread JSON objects, not the board JSON objects or
# catalog JSON objects.

quotelinks-of = require \./quotelinks-of

module.exports = (data, board) -> new APIThread data, board

APIThread:: = require(\./Thread)::
!function APIThread {posts: [op, ...replies]}: data, board
  thumbs-base = "//t.4cdn.org/#board/"
  images-base = "//i.4cdn.org/#board/"
  thread-url  = "//boards.4chan.org/#board/thread/"
  @no  = op.no
  @url = thread-url + op.no

  @preview = !!op.omitted_posts
  @sticky  = !!op.sticky
  @closed  = !!op.closed

  @op      = new APIPost this, true, op, 0, images-base, thumbs-base
  idx = 1 + (op.omitted_posts || 0)
  @replies = for replies
    new APIPost this, false, &, idx++, images-base, thumbs-base

  @postprocess!

APIPost:: = require(\./Post)::
!function APIPost thread, @op, data, @idx, images-base, thumbs-base
  @<<< data{
    no
    subject: sub
    name
    tripcode: trip
    uid: id
    capcode
  }

  @thread-no     = thread.no

  @url           = "#{thread.url}\#p#{data.no}"
  @time          = new Date data.time * 1000

  @comment       = data.com or ''

  @deleted-image = !!data.filedeleted

  @quotelinks    = quotelinks-of @comment, @thread-no
  @backlinks     = []

  if data.fsize
    @thumb-url    = thumbs-base + data.tim + \s.jpg
    @thumb-width  = data.tn_w
    @thumb-height = data.tn_h

    @image-url      = images-base + data.tim + data.ext

    @image-width    = data.w
    @image-height   = data.h

    @image-size     = humanized data.fsize
    @image-filename = data.filename + data.ext
    @image-md5      = data.md5

    @spoiler  = !!data.spoiler

# 4chan format image sizes like this on its HTML pages.
# TODO this might be better to do during templating actually.
function humanized bytes
  if bytes < 1024
    "#bytes B"
  else if (kbytes = Math.round bytes / 1024) < 1024
    "#kbytes KB"
  else
    "#{(kbytes / 1024)toString!substring 0 3} MB"
