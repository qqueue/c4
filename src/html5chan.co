# html5chan, main code entry point

# XXX most of these `require`s calls just run side effects, due to legacy code
# structure. Should rethink flow

console.group "html5chan"

console.timeStamp "html5chan-init"
console.time "init"
console.time "interactive"

# `board` holds global state about the current page, like `document` for the
# 4chan domain.
window.board = {}
  # boards.4chan.org/<name>/'res' or <page>/<threadid>
  [ , &name, page, &thread-no] = window.location.pathname / \/

  &is-thread   = !!&thread-no
  &is-board    = not &is-thread
  &page        = parseInt page, 10 or 0

  &url         = "//boards.4chan.org/#{&name}/"
  &threadurl   = &url + \res/
  &thread-path = "/#{&name}/res/#{&thread-no}"

  &ready       = false # flag similar to document.readyState

  &favicons = require \./favicon-data

require \./archives
require \./poster
require \./updater
require \./onready

# features
# TODO why `forcequotes` is a feature and `enhancer` isn't is kind of
# arbitrary. Should think more about naming and directory structure.
#
# Would be nicer if features could register themselves without explicit `require`,
# but such is the nature of node modules as opposed to concatenation, I guess.
require \./features/forcequotes
require \./features/hide-motd
require \./features/hide
require \./features/highlight
require \./features/image-expansion
require \./features/image-previews
require \./features/inlinereplies
require \./features/postpreviews
require \./features/quote
require \./utils/relative-dates
require \./features/relative-dates
require \./features/youtube

{onready} = require \./utils/features

onready !->
  console.timeEnd "onready handlers"
  console.timeEnd "interactive"
  console.timeStamp "html5chan-loaded"
  console.groupEnd!

console.timeEnd "init"
