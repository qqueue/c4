# adds features at the comment html level
		
# taking the regex approach with innerHTML
# because I believe that cloning the element and working with the DOM
# before taking the HTML would be slower
parser.enhance = ->
	it.innerHTML 
		# strip 4chan's old js-based spoilers and old reply highlights
		.replace /onmouseover="this\.style\.color='#FFF';" onmouseout="this\.style\.color=this\.style\.backgroundColor='#000'" style="color:#000;background:#000"|onclick="replyhl\('\d+'\);"/g ''
		
		# unsplits urls (probably) split at 100 characters
		.replace /([\w\-\._~:\/?#\[\]\(\)@!$&'*+,;=%]{100,})<br>/g '$1'

		#youtube thumbnails
		.replace do 
			//
				(?:https?:\/\/)? # optional protocol
				(
					youtu\.be\/([\w\-_]+)(\?[&=\w\-_;\#]*)? | # short links
					(?:www\.)youtube\.com\/watch\?
						([&=\w\-_;\?\#]*) # pre video id options (annoying, since this will have to give up matches)
						v=([\w\-_]+) # video id
						([&=\w\-_;\?\#]*) # post vieo id options
				)
			//g
			'<a href="http://$1" class="youtube" data-id="$2$5" data-params="$3$4$6" target="_blank" style="display: inline-block; width: 120px; height: 90px; background-image: url(\'http://img.youtube.com/vi/$2$5/2.jpg\')"></a>'

		# specialcase linkify urls without internal parenthesis surrounded by
		# parenthesis like (http://google.com)
		.replace do
			/\((https?:\/\/)([^<\s\)]+)\)/g ' 
			<a class="external" rel="noreferrer" href="$1$2" title="$1$2" target="_blank">[$2]</a> '

		# linkify links not preceded by a quote or double-quote (should avoid relinkifying href= urls)
		.replace do
			/([^"'(]|^)(https?:\/\/)([^<\s]+)/g
			'$1<a class="external" rel="noreferrer" href="$2$3" title="$2$3" target="_blank">[$3]</a>'

		# specialcase linkify urls without a protocol but with a common tld
		.replace do
			/(^|>|\s)([\w\.\-]+\.(?:com|net|org|jp(?!g))[^<\s]*)/g
			'$1<a class="external" rel="noreferrer" href="http://$2" title="$2" target="_blank">[$2]</a>'

		# attempt to redirect broken post links to the archive
		# uses ultra clever atomic group mimickry with lookahead
		# http://blog.stevenlevithan.com/archives/mimic-atomic-groups
		# not preceded by ; to avoid grabbing stuff like '>>>9gag.com'
		.replace /(^|[^;])&gt;&gt;(?=(\d+))\2(?!<\/a>)/g board.archivelink
