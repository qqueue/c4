toggleExpand = (e) ->
  unless e.altKey or e.ctrlKey or e.shiftKey or e.metaKey
    expand this
    e.preventDefault!

expand = (a) ->
  a.hidden = true
  a.before kup \img src: a.href, className: \full, ->
    @on \click -> collapse a
  $ \imgpreview ?.remove!

collapse = (a, scroll = true) ->
  a.hidden = false
  a.previousSibling?remove! # the full image

  # top of thumb is above scroll
  if scroll and (top = a.getBoundingClientRect!.top) < 0
    window.scrollBy 0, top # scroll so it is visible

preloading = false
preloadAll = ->
  for a of document.querySelectorAll \a.file
    kup \img src: a.href # creating an image forces browser to fetch

expanding = false
expandAll = -> expand img for img of $$ \a.file
collapseAll = -> collapse img, false for img of $$ \a.file

changeDisplay = ->
  set \img-display @value
  $ \img-style .textContent =
    if @value is \full
      ""
    else
      "img.full { max-width: 100%; }"

<-! onready

document.head.appendChild kup \style ->
  @id = \img-style
  @textContent =
    if get \img-display is \full
      ""
    else
      "img.full { max-width: 100%; }"

listen $ \threads
  .on \click \a.file toggleExpand

listen $ \preload-all .on \click ->
  ! := preloading
  if preloading
    preloadAll!
    @textContent = "Stop Preloading"
  else
    @textContent = "Preload all"

listen $ \collapse-all .on \click collapseAll

listen $ \expand-all .on \click ->
  ! := expanding
  if expanding
    expandAll!
    preloading = false
    @textContent = "Stop expanding"
  else
    @textContent = "Expand all"

listen $ \img-display .on \change changeDisplay

onupdate !->
  preloadAll! if preloading
  if expanding
    expand img for img of $$ '.new a.file'
