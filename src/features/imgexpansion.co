# Wonderful server-killing image expansion tools

expand = !(a) ->
  return if a.hidden # already expanded
  a.hidden = true
  a.before with document.createElement \img
    &src =  a.href
    &className = \full
    &onclick = !-> collapse a
  $ \imgpreview ?.remove!

collapse = !(a, scroll = true) ->
  a.hidden = false
  a.previousSibling?remove! # the full image

  # top of thumb is above scroll
  if scroll and (top = a.getBoundingClientRect!.top) < 0
    window.scrollBy 0, top # scroll so it is visible

# force browser fetch by creating img element
preload = !(a) -> with document.createElement \img => &src = a.href

preloading = false
expanding = false

onclick = !(e) ->
  unless e.altKey or e.ctrlKey or e.shiftKey or e.metaKey
    expand this
    e.preventDefault!

onready !->

  # manage single-rule stylesheet so we don't have to iterate over elements
  fit-width = with document.createElement \style
    &textContent = '.full { max-width: 100%; }'
  document.head.append fit-width

  fit-width.disabled = get(\img-display) is \full

  listen $ \img-display .on \change !->
    set \img-display @value
    fit-width.disabled = get(\img-display) is \full

  listen $ \preload-all .click ->
    if ! := preloading
      preload img for img of $$ '.file'
      @textContent = "Stop Preloading"
    else
      @textContent = "Preload all"

  listen $ \expand-all .click ->
    if ! := expanding
      preloading := false
      expand img for img of $$ '.file'
      @textContent = "Stop expanding"
    else
      @textContent = "Expand all"

  listen $ \collapse-all .click !->
    for $$ '.full'
      &nextSibling.hidden = false
      &remove!


  file.addEventListener \click onclick for file of $$ \.file

onupdate !->
  if preloading => for $$ '.new .file' => preload &
  if expanding  => for $$ '.new .file' => expand  &

onpostinsert !->
  it.detail.post.querySelector \.file ?.addEventListener \click onclick

