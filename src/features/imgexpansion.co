toggleExpand = (e) ->
	unless e.altKey or e.ctrlKey or e.shiftKey or e.metaKey
		expand this
		return false

expand = (a) ->
	unless a.previousSibling?tagName is \IMG
		# hiding a block-level anchor doesn't hide children in ff ;_;
		a.firstElementChild.hidden = true
		insertBefore a, kup \img src: a.href, className: \full, ->
			@on \click -> collapse a
		remove document.getElementById \imgpreview

collapse = (a, scroll = true) ->
	a.firstElementChild.hidden = false
	remove a.previousSibling
	if scroll and (top = a.getBoundingClientRect!.top) < 0 # top of thumb is above scroll
		window.scrollBy 0, top # scroll so it is visible

preloading = false

preloadAll = -> 
	for a of document.querySelectorAll \a.file
		kup \img src: a.href #creating an image forces browser to fetch

expanding = false
expandAll = -> expand img for img of $$ \a.file

collapseAll = -> 
	collapse img, false for img of $$ \a.file

changeDisplay = ->
	set \img-display @value
	document.getElementById \img-style .textContent = switch @value
		case \full then ""
		case \fit then "img.full { max-width: 100%; }"

features.imgexpansion =
	on: ->
		document.head.appendChild create \style ->
			@id = \img-style
			@textContent = switch get \img-display
				case \full then ""
				case \fit then "img.full { max-width: 100%; }"
				default then "img.full { max-width: 100%; }"

		listen document.getElementById \threads
			.on \click \a.file toggleExpand
		listen document.getElementById \preload-all .on \click ->
			! := preloading
			if preloading
				preloadAll!
				@textContent = "Stop Preloading"
			else
				@textContent = "Preload all"
		listen document.getElementById \collapse-all .on \click collapseAll
		listen document.getElementById \expand-all .on \click ->
			! := expanding
			if expanding
				expandAll!
				preloading = false
				@textContent = "Stop expanding"
			else
				@textContent = "Expand all"
		listen document.getElementById \img-display .on \change changeDisplay

		updater.after_update ->
			preloadAll! if preloading
			expandAll! if expanding

	off: ->
		listen document.getElementById \threads
			.off \click toggleExpand
		# TODO off stuff
