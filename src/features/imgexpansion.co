# Wonderful server-killing image expansion tools

expand = !(a) ->
  return if a.hidden # already expanded
  a.hidden = true
  a.before kup \img src: a.href, className: \full, !->
    @on \click !-> collapse a
  $ \imgpreview ?.remove!

collapse = !(a, scroll = true) ->
  a.hidden = false
  a.previousSibling?remove! # the full image

  # top of thumb is above scroll
  if scroll and (top = a.getBoundingClientRect!.top) < 0
    window.scrollBy 0, top # scroll so it is visible

# force browser fetch by creating img element
preload = !(a) -> kup \img src: a.href

preloading = false
expanding = false

onready !->

  # manage single-rule stylesheet so we don't have to iterate over elements
  fit-width = kup \style '.full { max-width: 100%; }'
  document.head.append fit-width

  fit-width.disabled = get(\img-display) is \full

  listen $ \img-display .on \change !->
    set \img-display @value
    fit-width.disabled = get(\img-display) is \full

  # TODO think about 'custom' event plain-click or unmodified-click to abstract
  # the unless statement
  for file of $$ \.file
    file.addEventListener \click !(e) ->
      unless e.altKey or e.ctrlKey or e.shiftKey or e.metaKey
        expand this
        e.preventDefault!

  listen $ \preload-all .click ->
    if ! := preloading
      preload img for img of $$ '.file'
      @textContent = "Stop Preloading"
    else
      @textContent = "Preload all"

  listen $ \expand-all .click ->
    if ! := expanding
      preloading := false
      expand img for img of $$ '.file'
      @textContent = "Stop expanding"
    else
      @textContent = "Expand all"

  listen $ \collapse-all .click !-> collapse img for img of $$ '.full'

onupdate !->
  preload img for img of $$ '.new .file' if preloading
  expand  img for img of $$ '.new .file' if expanding

