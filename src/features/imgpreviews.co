<- onready
<- listen $ \threads .on \mouseover 'img.thumb' 

remove $ \imgpreview # just in case

width = @parentElement.getAttribute \data-width
height = @parentElement.getAttribute \data-height
src = @parentElement.href

docheight = window.innerHeight - 20
# using clientWidth to account for scrollbars
# can't use it for height because it would be the entire
# document height
# plus, ff uses quirks mode on 4chan:
# http://code.google.com/p/doctype-mirror/wiki/ArticleViewportSize
docwidth = document.documentElement.clientWidth - 20

ratio = Math.min 1, docheight/height, docwidth/width
width *= ratio
height *= ratio

l = $ \lightbox
l.classList.add \dark
l.appendChild kup \img {id: \imgpreview alt: "Loading...", src, width, height } ->
	@on \load -> @removeAttribute \alt
	@on \error -> @alt = "Unable to load image."

	@css do
		position: \absolute
		top: 10 + (docheight - height) / 2
		left: 10 + (docwidth - width) / 2

listen this .on \mouseout function destroy
	remove $ \imgpreview
	$ \lightbox .classList.remove \dark
	listen this .off \mouseout destroy

