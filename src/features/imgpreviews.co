constrain = (el, e) ->
	return unless el and e
	height = el.naturalHeight
	width = el.naturalWidth
	docheight = window.innerHeight - 20
	availableheight = docheight - e.clientY
	docwidth = window.innerWidth - 20
	availablewidth = docwidth - e.clientX
	
	ratio = Math.min 1, docheight/height, availablewidth/width

	el.style.width = width * ratio
	el.style.height = height * ratio

	el.style.left = e.clientX + 10
	if (height * ratio ) > availableheight
		el.style.bottom = 10
		el.style.removeProperty \top
	else
		el.style.top = e.clientY + 10

make = (e) ->
	src = @parentElement.href
	remove document.getElementById \imgpreview
	create \img ->
		@id = \imgpreview
		@src = src
		@alt = "Loading..."
		listen this
			.on \load ->
				@removeAttribute \alt
				@style.opacity = 1
			.on \error -> @alt = "Unable to load image."
		constrain this, e
		@style.opacity = 0.7
		@style.position = \fixed
		document.body.appendChild this

move = (e) -> constrain document.getElementById(\imgpreview), e

destroy = (e) -> remove document.getElementById \imgpreview

# image hover previews
features.imgpreviews = 
	on: ->
		listen document.getElementById \threads
			.on \mouseenter 'img.thumb' make
			.on \mousemove 'img.thumb' move
			.on \mouseleave 'img.thumb' destroy
	off: ->
		listen document.getElementById \threads
			.off \mouseenter make
			.off \mousemove move
			.off \mouseleave destroy
