# show a centered image preview when hovering over an image after a short
# delay, and only remove it after the mouse moves out of the dead zone this is
# similar to how tooltips are handled in firefox.
const delay     = 200ms
const dead-zone = 10px

export handle-preview = !({clientX: x, clientY: y}) ->
  {width, height} = @parentElement.dataset
  src = @parentElement.href

  # http://code.google.com/p/doctype-mirror/wiki/ArticleViewportSize
  # size of viewport excluding scrollbars
  docheight = document.documentElement.clientHeight - 20
  docwidth  = document.documentElement.clientWidth - 20

  ratio = Math.min 1, docheight/height, docwidth/width
  width  *= ratio
  height *= ratio

  l = $ \lightbox

  var timeout
  # until the image is displayed, continually reset
  # timeout on mousemovement
  reset-image = !(e) ->
    clearTimeout timeout
    timeout := setTimeout create-image, delay

    # store the new current mouse position
    x := e.clientX
    y := e.clientY

  var full
  create-image = !~>
    l.classList.add \dark
    full ||:= document.createElement \img
      &alt = "Loading..."
      & <<< {src, width, height}

      &addEventListener \load  -> @removeAttribute \alt
      &addEventListener \error -> @alt = "Unable to load image."

      &style <<<
        position: \absolute
        top:  "#{10 + (docheight - height) / 2}px"
        left: "#{10 + (docwidth  - width ) / 2}px"

    l.appendChild full
    @classList.add \hovered-img

    # swap creation with destruction
    listen this
      .off \mousemove reset-image
      .on \mousemove remove-image

  remove-image = !({clientX: cx, clientY: cy}) ->
    if Math.abs(x - cx) > dead-zone or Math.abs(y - cy) > dead-zone
      l.classList.remove \dark
      @classList.remove \hovered-img

      full?remove!

      # restart tooltip timeout
      timeout := setTimeout create-image, delay
      listen this
        .on \mousemove reset-image
        .off \mousemove remove-image

  # start initial timeout
  timeout = setTimeout create-image, delay

  # add handlers
  listen this
    .on \mousemove reset-image
    .once \mouseout !->
      full?remove!
      clearTimeout timeout # stop image from appearing
      l.classList.remove \dark
      listen this
        .off \mousemove reset-image
        .off \mousemove remove-image

onready !->
  console.time 'attach thumb hover listeners'
  for $$ \.thumb => &addEventListener \mouseover handle-preview
  console.timeEnd 'attach thumb hover listeners'

onpostinsert !->
  it.detail.post.querySelector \.thumb ?
    .addEventListener \mouseover handle-preview

