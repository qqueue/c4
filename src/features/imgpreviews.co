# show a centered image preview when hovering over an image after a short delay

# scaled width and height from original width and height. manual version of CSS
# object-fit (only in opera)
object-fit = (container, width, height) ->
  ratio = Math.min do
    1
    container.height / height
    container.width  / width

  width  : ratio * width
  height : ratio * height

export handle-preview = tooltip do
  show: !->
    @style.cursor = \none # hide cursor while hovering

    a = @parentElement

    # http://code.google.com/p/doctype-mirror/wiki/ArticleViewportSize
    # size of viewport excluding scrollbars.
    viewport =
      width  : document.documentElement.clientWidth
      height : document.documentElement.clientHeight

    document.body.append with document.createElement \img
      &id = \imgpreview
      &alt = "Loading..."
      &src = a.href

      &{width, height} = object-fit do
        viewport
        a.dataset.width
        a.dataset.height

      &addEventListener \load  -> @removeAttribute \alt
      &addEventListener \error -> @alt = "Unable to load image."

      &style <<<
        position            : \fixed
        left                : 0
        top                 : 0
        pointer-events      : \none

        # give a lightbox-like shading to the page
        background-color    : 'rgba(0,0,0,.5)'

        # pad out the background-color to the entire viewport
        padding             : "#{(viewport.height - &height) / 2}px \
                               #{(viewport.width  - &width ) / 2}px"

        transition-duration : \.5s
        opacity             : 0

      # remove once faded completely.
      &addEventListener \transitionend ({propertyName}: e) ->
        if propertyName is \opacity and @style.opacity is '0'
          @remove!

      # fade in, deferred to trigger transition animation.
      defer 100ms !-> &style.opacity = 1

  hide: !->
    # fade out. would be nice if DOM removal triggered CSS opacity transitions,
    # but it doesn't.
    $ \imgpreview ?.style.opacity = 0

    @style.removeProperty \cursor

onready !->
  console.time 'attach thumb hover listeners'
  for $$ \.thumb => &addEventListener \mouseover handle-preview
  console.timeEnd 'attach thumb hover listeners'

onpostinsert !->
  it.detail.post.querySelector \.thumb ?
    .addEventListener \mouseover handle-preview

