# FIXME this sucks
<- onready
listen $ \threads .on \click \button.expand ->
	button = this
	id = @value
	preview = $ "thread-#id"
	with new XMLHttpRequest
		@open \GET Thread[id].url
		listen this 
			.on \load ->
				html = document.implementation.createHTMLDocument ''
				html.documentElement.innerHTML = @response

				#
				{thread} = parser.yotsuba html, preview: false, threadPath: "/#{board.name}/res/#id"
				preview.parentNode.replaceChild do
					# FIXME kinda messy
					with document.createElement \div
						@innerHTML = thread.render!
						@firstElementChild
					preview
				# update things
				fn! for fn of updater.callbacks
			.on \error ->
				button.textContent = "Couldn't load thread!"
				button.disabled = true
		@send!
