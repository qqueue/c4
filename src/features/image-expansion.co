# embed full image on click

expand = !(a) ->
  return if a.hidden # already expanded
  a.hidden = true
  a.before with document.createElement \img
    &src =  a.href
    &className = \full
    &style <<<
      display: \block
      max-width: \100%
    &onclick = !->
      # if the image is scaled down, first cycle to full size, then remove.
      # This is a good comprimize to having a selector between "fit-width" and
      # "100%" globally.
      if @width is not @naturalWidth
        @style.removeProperty \max-width
      else
        collapse a
  $ \imgpreview ?.remove!

collapse = !(a, scroll = true) ->
  a.hidden = false
  a.previousSibling?remove! # the full image

  # top of thumb is above scroll
  if scroll and (top = a.getBoundingClientRect!.top) < 0
    window.scrollBy 0, top # scroll so it is visible

onclick = !(e) ->
  unless e.altKey or e.ctrlKey or e.shiftKey or e.metaKey
    expand this
    e.preventDefault!

onready !->
  for $$ \.file => &addEventListener \click onclick

onpostinsert !->
  it.detail.post.querySelector \.file ?.addEventListener \click onclick

