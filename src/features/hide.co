# hide threads and replies (posts)
# TODO refactor stub functionality out of CSS
# can then add things like 'hide similar' 'hide replies' etc
#
# Uses 4chan's "native" localStorage hidden hash, so hidden threads are
# persisted to the 4chan catalog.

{$$} = require \../utils/dom
{onready, onupdate} = require \../utils/features
Thread = require \../classes/Thread

const threshold = 604800000ms # one week

hidden =
  threads: try JSON.parse localStorage"4chan-hide-t-#{board.name}" or {} catch {}
  replies: try JSON.parse localStorage"4chan-hide-r-#{board.name}" or {} catch {}

console.log hidden

# delete expired entries to keep object small
let now = Date.now!
  for type, hash in hidden
    for key, expiry in hash
      # 4cat catalog sets thread hash values to "true", so change those to
      # real timestamps
      if expiry is true
        hash[key] = Date.now!
      else
        delete hash[key] if (now - expiry) > threshold

persist = !->
  localStorage"4chan-hide-t-#{board.name}" = JSON.stringify hidden.threads
  localStorage"4chan-hide-r-#{board.name}" = JSON.stringify hidden.replies

# perform actual DOM manipulation
toggle = (prefix, no) ->
  # all referencing quotelinks
  for $$ ".quotelink[href$=\"##no\"]" => &classList.toggle \hiddenlink
  $ "#prefix#no" ?.classList.toggle \hidden

onready !->
  for $$ '.reply button.hide'
    &addEventListener \click !->
      toggle \p @value
      if @value in hidden.replies
        delete hidden.replies[@value]
      else
        hidden.replies[@value] = Date.now!
      persist!

  for btn of $$ '.op button.hide'
    btn.addEventListener \click !->
      toggle \t @value
      if @value in hidden.threads
        delete hidden.threads[@value]
      else
        # use Thread hash to always persist sticky threads
        hidden.threads[@value] = if Thread[@value]?sticky
          Number.MAX_VALUE
        else
          Date.now!
      persist!

  # hide all previously hidden replies and threads
  # TODO do this at render time instead of post render for faster display
  for document.getElementsByClassName \reply
    no = &dataset.no
    toggle \p no if hidden.replies[no]

  if board.is-board # don't bother on the actual thread page
    for document.getElementsByClassName \thread
      no = &dataset.no
      toggle \t no if hidden.threads[no]

onupdate !->
  for $$ ".new .quotelink"
    if &hash.substring(1) in hidden.replies
      &classList.toggle \hiddenlink

