# update relative post times periodically

# each time element adds itself to the stale list once it becomes inaccurate.
# the stale list is flushed and stale times are updated whenever the DOM
# changes otherwise, which keeps the times as up to date as possible without
# incurring separate DOM reflows.
stale = []

debounce-leading = (delay, fn) ->
  var timeout
  reset = !-> timeout := null
  -> unless timeout
    fn ...
    timeout := defer delay, reset

flush = debounce-leading SECOND, !->
  now = Date.now!
  for stale => & now
  stale.length = 0

keep-up-to-date = !(el) ->
  time = new Date(el.getAttribute \datetime)

  update = !(now) ->
    el.textContent = time.relative-time now
    make-timeout now - time.getTime!

  add-to-stale = !-> stale.push update

  make-timeout = !(diff) ->
    # calculate when relative date will be stale again
    delay = if diff > DAY
      diff % DAY
    else if diff > HOUR
      diff % HOUR
    else if diff > MINUTE
      diff % MINUTE
    else
      SECOND
    defer delay, add-to-stale

  make-timeout Date.now! - time.getTime!

onpostinsert !->
  keep-up-to-date it.detail.post.querySelector \time

  # take advantage of DOM reflow
  flush!

# flush when user switches back to the tab
window.addEventListener \visibilitystatechange !->
  flush! unless document.hidden

