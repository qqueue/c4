# these two functions keep the mouse in the same
# relative position on the document when content
# above the mouse is removed or added
removeAndScroll = (removed, relativeTo) ->
	[oldx,oldy] = [relativeTo.offsetLeft, relativeTo.offsetTop]
	remove removed
	[newx,newy] = [relativeTo.offsetLeft, relativeTo.offsetTop]
	unless window.scrollY is window.scrollMaxY
		window.scrollBy newx-oldx, newy-oldy

insertAndScroll = (inserted, relativeTo) ->
	[oldx,oldy] = [relativeTo.offsetLeft, relativeTo.offsetTop]
	relativeTo.parentElement.insertBefore inserted, relativeTo
	[newx,newy] = [relativeTo.offsetLeft, relativeTo.offsetTop]
	window.scrollBy newx-oldx, newy-oldy

# inline replies
toggle = (e) ->
	if post = Post[id = @hash.substring 1]
		host = closest \.post, this .id
		hostid = host.split \- .pop! # grab last (if nested inline post)
		inlined_id = "#host-#id" # id is unique to hosting post and inlined post
		if inlined = document.getElementById inlined_id # toggle off
			removeAndScroll inlined, this
			@classList.remove \inlinedlink
		else
			inlined = post.element \article 'inline hovered' inlined_id
			for link of inlined.querySelectorAll "a.quotelink[href$=\"##hostid\"]" # replace matching reply link
				link.outerHTML = """<strong class="recursivelink">#{link.innerHTML}</strong>"""
			wrapper = this # walk outside of inline elements like spoilers and greentext
			wrapper.=parentElement while wrapper.parentElement[matchesSelector] 'a,span,font'
			if @classList.contains \backlink
				@parentElement.insertBefore inlined, wrapper.nextSibling
			else
				insertAndScroll inlined, wrapper
			@classList.add \inlinedlink
			remove document.getElementById \postpreview
		return false

bypass = -> # bypass inline replies on dblclick
	window.location.hash = that if @hash  # actually follow link

features.inlinereplies =
	on: ->
		listen document.getElementById \threads
			.on \click 'a.quotelink:not(.hiddenlink)' toggle
			.on \dblclick \a.quotelink bypass
	off: ->
		listen document.getElementById \threads
			.off \click toggle
			.off \dblclick bypass
