
# inline replies
listen document.getElementById \threads
	.on \click 'a.quotelink:not(.hiddenlink)' (e) ->
		if post = Post[id = @hash.substring 1]
			$this = $ this
			host = $this.closest \.post .attr \id
			hostid = host.split \- .pop! # grab last (if nested inline post)
			inlined_id = "#host-#id" # id is unique to hosting post and inlined post
			if (inlined = $ "##inlined_id").exists! # toggle off
				inlined.removeAndScroll $this
				$this.removeClass \inlinedlink
			else
				inlined = $ post.render \article 'inline hovered' inlined_id
					.find "a.quotelink[href$=\"##hostid\"]" # replace matching reply link
					.replaceWith ->
						$ \<strong> class: \recursivelink
							.html $(this).html!
							.addClass @className
					.end!
				if $this.is \.backlink
					$this.after inlined
				else
					$this.beforeAndScroll inlined
				$this
					.addClass \inlinedlink
					.trigger \mouseleave.html5chan.postpreview # since we're previewing it now
			return false
	.on \dblclick \a.quotelink -> # bypass inline replies on dblclick
		window.location.hash = that if @hash  # actually follow link
