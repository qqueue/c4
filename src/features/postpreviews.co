# post hover previews

# XXX also will break weirdly if there is a cross-board link to a board with
# similar post numbers.  IDs will start to be overwritten since there are only
# unique within a board.
fetch-new-post = !(id) ->
  [, board, , thread] = @pathname.split \/ # /<board>/res/<thread>

  link = this
  @style.cursor = \progress

  # TODO guard against multiple requests in short time period
  xhr = new XMLHttpRequest
    &open \GET "//api.4chan.org/#board/res/#thread.json"
    &onload = !->
      # TODO handle 404 -> archive
      if @status is 200
        thread = parser.api JSON.parse @response
        console.log thread

        if still-hovered
          link.style.removeProperty \cursor

          # Post[] hash should now be updated
          # XXX kind of bad global state, should change how this works
          create-preview.call link, id, Post[id]

    &send!

  still-hovered = true

  @addEventListener \mouseout function out
    still-hovered := false
    @style.removeProperty \cursor
    @removeEventListener \mouseout out

handle-preview = !->
  return if @classList.contains \inlinedlink \
         or @classList.contains \recursivelink

  id = @hash.substring 2 # "#p"

  if not post = Post[id]
    fetch-new-post.call this, id
  else
    create-preview.call this, id, post

create-preview = !(id, post) ->
  $ \postpreview ?.remove!

  host = closest \.post this
  hostid = (host.id / \-)pop!

  {width, height, left, top} = @getBoundingClientRect!

  preview = post.element \article, , \postpreview

    classify (&querySelectorAll ".quotelink[href$=\"#hostid\"]")
      .add \recursivelink

    &style
      &position = \fixed
      if left > (docWidth = document.documentElement.clientWidth) / 2
        &right = "#{docWidth - left - width}px"
      else
        &left = "#{left}px"

      if @classList.contains \backlink
        &top = "#{top + height + 5}px"
      else
        &bottom = "#{window.innerHeight - top + 5}px"

    document.body.appendChild (&)

  document.dispatchEvent new CustomEvent do
    \html5chan-postinsert
    detail: post: preview

  # highlight hovered replies
  classify $$ ".post[data-id=\"#id\"]" .add \hovered

  listen this .once \mouseout !->
    preview.remove!
    classify $$ ".post[data-id=\"#id\"]" .remove \hovered

onready !->
  console.time 'attach post hover listeners'
  for $$ \.quotelink => &addEventListener \mouseover handle-preview
  console.timeEnd 'attach post hover listeners'

onpostinsert !->
  for it.detail.post.querySelectorAll \.quotelink
    &addEventListener \mouseover handle-preview

onbacklink !({detail}) ->
  <-! defer 100ms
  detail.post.querySelector ".backlink[href$=p#{detail.id}]"
    &addEventListener \mouseover handle-preview

