# post hover previews
<- onready
<- listen $ \threads .on \mouseover 'a.quotelink'

if @hash
	remove document.getElementById \postpreview

	host = closest \.post this

	hostid = host.id.split \- .pop!
	# if preview exists, and link is not inlined
	if (post = Post[id = @hash.substring 1]) and not @classList.contains \inlinedlink
		preview = post.element \article, , \postpreview
		for link of preview.querySelectorAll "a.quotelink[href$=\"##hostid\"]" # highlight
			link.classList.add \recursivelink

		bounds = @getBoundingClientRect!

		preview.style.position = \fixed
		if bounds.left > (width = document.documentElement.clientWidth) / 2
			preview.style.right = width - bounds.left - bounds.width
		else
			preview.style.left = bounds.left
		if @classList.contains \backlink
				preview.style.top = bounds.top + bounds.height + 5
		else
			preview.style.bottom = window.innerHeight - bounds.top + 5

		document.body.appendChild preview

		listen this .on \mouseout function destroy
			remove preview
			listen this .off \mouseout destroy

	# highlight hovered replies
	for post of document.getElementsByClassName	"post#{@hash.substring 1}"
		post.classList.add \hovered

	listen this .on \mouseout function unhighlight
		for post of document.getElementsByClassName	"post#{@hash.substring 1}"
			post.classList.remove \hovered
		listen this .off \mouseout unhighlight
