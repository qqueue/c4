# post hover previews

handle-preview = !->
  return if @hash is '' or @classList.contains \inlinedlink

  id = @hash.substring 2 # "#p"
  return unless post = Post[id]

  $ \postpreview ?.remove!

  host = closest \.post this
  hostid = (host.id / \-)pop!

  {width, height, left, top} = @getBoundingClientRect!

  preview = post.element \article, , \postpreview

    classify (&querySelectorAll ".quotelink[href$=\"#hostid\"]")
      .add \recursivelink

    &style
      &position = \fixed
      if left > (docWidth = document.documentElement.clientWidth) / 2
        &right = "#{docWidth - left - width}px"
      else
        &left = "#{left}px"

      if @classList.contains \backlink
        &top = "#{top + height + 5}px"
      else
        &bottom = "#{window.innerHeight - top + 5}px"

    document.body.appendChild (&)

  document.dispatchEvent new CustomEvent do
    \html5chan-postinserted
    detail: {post: preview}

  # highlight hovered replies
  classify $$ ".post[data-id=\"#id\"]" .add \hovered

  listen this .once \mouseout !->
    preview.remove!
    classify $$ ".post[data-id=\"#id\"]" .remove \hovered

onready !->
  console.time 'attach post hover listeners'
  for link of $$ \.quotelink
    link.addEventListener \mouseover handle-preview
  console.timeEnd 'attach post hover listeners'

onpostinserted !->
  for link of it.detail.post.querySelectorAll \.quotelink
    link.addEventListener \mouseover handle-preview
