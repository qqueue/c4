# post hover previews
<-! onready

listen $ \threads .on \mouseover 'a.quotelink' !->
  return if @hash is '' or @classList.contains \inlinedlink

  id = @hash.substring 1
  return unless post = Post[id]

  $ \postpreview ?.remove!

  host = closest \.post this
  hostid = (host.id / \-)pop!

  {width, height, left, top} = @getBoundingClientRect!

  preview = post.element \article, , \postpreview

    classify (&querySelectorAll "a.quotelink[href$=\"##hostid\"]")
      .add \recursivelink

    &style
      &position = \fixed
      if left > (docWidth = document.documentElement.clientWidth) / 2
        &right = "#{docWidth - left - width}px"
      else
        &left = "#{left}px"

      if @classList.contains \backlink
        &top = "#{top + height + 5}px"
      else
        &bottom = "#{window.innerHeight - top + 5}px"

    document.body.appendChild (&)

  # highlight hovered replies
  classify $$ ".post#id" .add \hovered

  listen this .once \mouseout !->
    preview.remove!
    classify $$ ".post#id" .remove \hovered

