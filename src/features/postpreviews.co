# post hover previews
listen document.getElementById \threads
	.on \mouseenter 'a.quotelink:not(.inlinedlink):not(.hiddenlink)' (e) ->
		$ \#postpreview .remove! # just in case
		$ \.hovered .removeClass \hovered # clear highlights
		$this = $ this
		return if $this.is \.inlinedlink # don't need to preview if it's right there.
		hostid = $this.closest \.post .attr \id .split \- .pop!
		if post = Post[id = @hash.substring 1]
			preview = $ post.render \article, ,\postpreview
				.find "a.quotelink[href$=\"##hostid\"]" # replace matching reply link
				.replaceWith ->
					$ \<strong> class: \recursivelink
						.html $(this).html!
						.addClass @className
				.end!
				.appendTo('body')
			height = preview.height()
			width = preview.width()
			left = e.pageX + 10
			if left + width > $(window).width! 
				left = Math.max 10, e.pageX - 10 - width
			preview.css do
					position: \absolute
					left: left
					top: e.pageY + if $(this).is \.backlink then 10 else -30 - height
	.on \mousemove \a.quotelink (e) ->
		preview = $ \#postpreview
		height = preview.height()
		width = preview.width()
		left = e.pageX + 10
		if left + width > $(window).width! 
			left = Math.max 10, e.pageX - 10 - width
		preview.css do
				position: \absolute
				left: left
				top: e.pageY + if $(this).is \.backlink then 10 else -30 - height
	.on \mouseleave \a.quotelink (e) ->
		$ \#postpreview .remove!
	# highlight hovered reply
	.on 'mouseenter mouseleave' \a.quotelink (e) ->
		$this = $ this
		$ if $this.is \.inlinedlink then "##{$this.closest \.post .attr \id}-#{this.hash.substring 1}"
			.add this.hash 
			.toggleClass \hovered
	
