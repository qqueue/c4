# post hover previews
<-! onready

listen $ \threads .on \mouseover 'a.quotelink' !->
  return if @hash is '' or @classList.contains \inlinedlink

  id = @hash.substring 1
  return unless post = Post[id]

  $ \postpreview ?.remove!

  host = closest \.post this
  hostid = (host.id / \-)pop!

  preview = post.element \article, , \postpreview

  for link of preview.querySelectorAll "a.quotelink[href$=\"##hostid\"]"
    link.classList.add \recursivelink

  {width, height, left, top} = @getBoundingClientRect!

  preview.style
    &position = \fixed
    if left > (docWidth = document.documentElement.clientWidth) / 2
      &right = "#{docWidth - left - width}px"
    else
      &left = "#{left}px"

    if @classList.contains \backlink
      &top = "#{top + height + 5}px"
    else
      &bottom = "#{window.innerHeight - top + 5}px"

  document.body.appendChild preview

  # highlight hovered replies
  for post of document.getElementsByClassName "post#{@hash.substring 1}"
    post.classList.add \hovered

  listen this .once \mouseout !->
    preview.remove!
    for post of document.getElementsByClassName "post#{@hash.substring 1}"
      post.classList.remove \hovered

