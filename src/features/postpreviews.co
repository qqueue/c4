# post hover previews
<- onready

listen $ \threads .on \mouseover 'a.quotelink' ->
  return if @hash is '' or @classList.contains \inlinedlink

  id = @hash.substring 1
  return unless post = Post[id]

  $ \postpreview ?.remove!

""

  host = closest \.post this
  hostid = (host.id.split / \-)pop!

  preview = post.element \article, , \postpreview

  for link of preview.querySelectorAll "a.quotelink[href$=\"##hostid\"]"
    link.classList.add \recursivelink

  bounds = @getBoundingClientRect!

  preview.style.position = \fixed
  if bounds.left > (width = document.documentElement.clientWidth) / 2
    preview.style.right = "#{width - bounds.left - bounds.width}px"
  else
    preview.style.left = "#{bounds.left}px"
  if @classList.contains \backlink
    preview.style.top = "#{bounds.top + bounds.height + 5}px"
  else
    preview.style.bottom = "#{window.innerHeight - bounds.top + 5}px"

  document.body.appendChild preview

  # TODO extend `listen` for a `once` method or similar to handle the off
  listen this .on \mouseout function destroy
    preview.remove!
    listen this .off \mouseout destroy

  # highlight hovered replies
  for post of document.getElementsByClassName "post#{@hash.substring 1}"
    post.classList.add \hovered

  listen this .on \mouseout function unhighlight
    for post of document.getElementsByClassName "post#{@hash.substring 1}"
      post.classList.remove \hovered
    listen this .off \mouseout unhighlight
