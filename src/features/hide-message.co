# global message functionality ot free up vertical space after it's been read
#
# Concerns:
#
# - Toggle message display with a button
#   - Toggle button text should reflect display state
# - Persist message display state between sessions
# - Show _new_ messages regardless of previous message display state
#
{onready} = require \../utils/features
{set, get} = require \../utils/storage
{$} = require \../utils/dom
Bacon = require \baconjs

# probably not two messages with the same length
hash = -> it.length

<-! onready

if $ \message-container
  button  = $ \hide-message
  message = $ \message

  new-hash = hash message.textContent

  message.hidden = new-hash is get \hide-message

  # toggle concern
  message-hidden = Bacon.from-event-target button, \click
    .scan message.hidden, -> not it

  message-hidden.on-value !-> message.hidden = it
  message-hidden.on-value !->
    set \hide-message if it then new-hash else ''

  # button text concern
  message-hidden
    .map -> "#{if it then "Show" else "Hide"} News"
    .on-value !-> button.textContent = it

