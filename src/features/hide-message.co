# global message functionality ot free up vertical space after it's been read
#
# Concerns:
#
# - Toggle message display with a button
#   - Toggle button text should reflect display state
# - Persist message display state between sessions
# - Show _new_ messages regardless of previous message display state

{onready} = require \../utils/features
{set, get} = require \../utils/storage
{$} = require \../utils/dom

# bacon.js-alike implementation, for kicks, until browserification
class Observable
  ->
    @callbacks = []

  emit: (event) ->
    for @callbacks
      & event

  # onValue in bacon, but each makes more sense with `map` and others, and is
  # more like clojure's `doseq`
  each: -> @callbacks.push it

  map: (fn) ->
    with new EventStream
      @each ->
        &emit fn it

  scan: (seed, reduce) ->
    with new Property seed
      @each -> &emit reduce &value, it

class EventStream extends Observable
  -> super ...

  ..from-events = (el, event) ->
    with new EventStream
      el.addEventListener event, &~emit

class Property extends Observable
  (@value) ->
    super ...

  emit: (val) ->
    @value = val
    super ...

  # properties map to new properties
  map: (fn) ->
    with new Property fn @value
      @each -> &emit fn it

  assign: (obj, property) ->
    asn = !-> obj[property] = it
    @each asn

    # initialize if necessary
    asn that if @value?

<-! onready

if $ \message-container
  button  = $ \hide-message
  message = $ \message

  message.hidden = get \message-hidden

  # toggle concern
  message-hidden = EventStream.from-events button, \click
    .scan message.hidden, -> not it

  message-hidden.assign message, \hidden
  message-hidden.each !-> set \message-hidden it

  # button text concern
  message-hidden
    .map -> "#{if it then "Show" else "Hide"} News"
    .assign button, \textContent

  # show _new_ messages concern

  # unlikely to be 2 consecutive messages with the same length, so cheat
  hash = -> it.innerHTML.length

  previous = get \message-hash

  # XXX not that reactive
  if hash message is not previous
    message.hidden = false
    set \message-hash hash message

