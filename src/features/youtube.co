# youtube embedding and title fetching

const api-key = "AIzaSyCe5gXUv-EFyNMoESO8ONZnottbsd-2ayA"
const batch-size = 30
const rate = 5000ms # time between requests

request-queue = []
ready = true
queue = !(req) ->
  request-queue.push req

  req.addEventListener \loadend !->
    request-queue.shift! # remove self

    defer rate, !->
      if request-queue.0 # there are more
        that.send!
      else
        ready := true
  if ready
    ready := false
    req.send!

# video information cache
cache = {}

set-title = !(vid, data) ->
  vid.title = "#{data.statistics.viewCount} views.
                 \n\n
               #{truncate data.snippet.description, 200}"
  vid.dataset.title = data.snippet.title

# TODO detect videos whose data isn't returned
# and set some sort of error title
handle = !(vids) ->
  to-fetch = {}

  for vid of vids
    vid.addEventListener \click onclick
    if cache[vid.dataset.id]
      set-title vid, that
    else
      to-fetch[vid.dataset.id] = true

  batches = []
  batch = []
  for id in to-fetch
    batch.push id
    if batch.length is batch-size
      batches.push batch
      batch = []
  batches.push batch

  if batch.length > 0
    for b of batches
      let req = new XMLHttpRequest
        req.open \GET "https://www.googleapis.com/youtube/v3/videos
                    ?id=#{encodeURIComponent b}
                    &part=snippet%2C+statistics
                    &fields=items(id%2Csnippet%2Cstatistics)
                    &key=#{api-key}"
        req.addEventListener \load !->
          data = JSON.parse @response
          for v of data.items
            cache[v.id] = v

            for vid of $$ ".youtube[data-id=\"#{v.id}\"]"
              set-title vid, v

        req.addEventListener \error !->
          console.error "what happen" this

        queue req

# embed iframes
# TODO add close button somehow
onclick = !(e) ->
  unless e.altKey or e.ctrlKey or e.shiftKey or e.metaKey
    e.preventDefault!
    @replace with document.createElement \iframe
      &width = 560
      &height = 315
      &src = "//www.youtube.com/embed/#{@dataset.id}
             ?#{@dataset.params or ''}
             &amp;autoplay=1
             &amp;wmode=transparent"
      &frameborder = 0
      &allowfullscreen = ''

onready      !-> handle $$ \.youtube
onpostinsert !-> handle it.detail.post.querySelectorAll \.youtube

