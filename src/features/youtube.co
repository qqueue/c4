# TODO fix styling in chrome by switching to 
# background-image-based approach
# also makes the > play button easier without weird
# psuedoelements
# TODO add close button somehow
# embed youtube links on click
embed = (e) ->
	unless e.altKey or e.ctrlKey or e.shiftKey or e.metaKey
		@outerHTML = 
			"""<iframe width="560" height="315" src="http://www.youtube-nocookie.com/embed/#{@dataset.id}#{if @dataset.params then "?#that" else \?}&amp;autoplay=1" frameborder="0" allowfullscreen></iframe>"""
		return false

fetch_link = ->
	link = this # called as event handler
	unless @title # this prevents multiple xhrs from being created
		@title = 'Loading video title... (re-mouse over preview to refresh)'
		with new XMLHttpRequest
			@open \get "https://gdata.youtube.com/feeds/api/videos/#{link.dataset.id}?v=2&alt=json" 
			# fucking chrome and its problems with supporting html5
			# @responseType = \json
			listen this
				.on \load -> 
					data = JSON.parse @responseText
					link.title = 
						"
						#{data.entry.title.\$t}
						\n\n
						#{data.entry.'yt$statistics'.viewCount} views.
						\n\n
						#{data.entry.'media$group'.'media$description'.\$t}
						"
				.on \error -> link.title = "Youtube [#{link.dataset.id}]"
				.on \loadend -> listen link .off \mouseenter fetch_link
			@send!

features.youtube =
	on: ->
		listen document.getElementById \threads
			.on \click \a.youtube embed
			.on \mouseenter \a.youtube fetch_link

	off: ->
		listen document.getElementById \threads
			.off \click embed
