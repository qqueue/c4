# youtube embedding and title fetching

const api-key = "AIzaSyCe5gXUv-EFyNMoESO8ONZnottbsd-2ayA"
const batch-size = 30
const rate = 5000ms # time between requests

request-queue = []
ready = true
queue = !(req) ->
  request-queue.push req

  req.addEventListener \loadend !->
    request-queue.shift! # remove self

    defer rate, !->
      if request-queue.0 # there are more
        that.send!
      else
        ready := true
  if ready
    ready := false
    req.send!

# video information cache
cache = {}

set-title = !(vid, data) ->
  vid.title = "#{data.statistics.viewCount} views.
                 \n\n
               #{truncate data.snippet.description, 200}"
  vid.dataset.title = data.snippet.title

# post insert adds any found videos to this, which is flushed by load-info
pending-videos = []

# TODO detect videos whose data isn't returned
# and set some sort of error title
#
# debounced so postinsert can call it without the initial postinsert rush at
# onready sending out one request per video
load-info = debounce 2000ms !->
  to-fetch = {}

  for vid of pending-videos
    vid.addEventListener \click onclick
    if cache[vid.dataset.id]
      set-title vid, that
    else
      to-fetch[vid.dataset.id] = true

  pending-videos := []

  batches = []
  batch = []
  for id in to-fetch
    batch.push id
    if batch.length is batch-size
      batches.push batch
      batch = []
  batches.push batch

  if batch.length > 0
    for b of batches
      let req = new XMLHttpRequest
        req.open \GET "https://www.googleapis.com/youtube/v3/videos
                    ?id=#{encodeURIComponent b}
                    &part=snippet%2C+statistics
                    &fields=items(id%2Csnippet%2Cstatistics)
                    &key=#{api-key}"
        req.addEventListener \load !->
          if 200 <= @status < 400
            data = JSON.parse @response
            for v of data.items
              cache[v.id] = v

              for vid of $$ ".youtube[data-id=\"#{v.id}\"]"
                set-title vid, v
          else
            console.error "error fetching youtube info!" this

        req.addEventListener \error !->
          console.error "what happen" this

        queue req

# embed iframes
# TODO add close button somehow
onclick = !(e) ->
  unless e.altKey or e.ctrlKey or e.shiftKey or e.metaKey
    e.preventDefault!
    @replace with L \iframe
      &width = 560
      &height = 315
      &src = "//www.youtube.com/embed/#{@dataset.id}
             ?#{@dataset.params or ''}
             &amp;autoplay=1
             &amp;wmode=transparent"
      &frameborder = 0
      &allowfullscreen = ''

onpostinsert !->
  pending-videos.push ...it.detail.post.querySelectorAll \.youtube
  load-info!

