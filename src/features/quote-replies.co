# add quote text to >> replies to be able to identify them without hover

munge = !(ctx) ->
  for quote of ctx.querySelectorAll '.quotelink:not(.backlink):not(.forcequoted)'
    # these capcodeReplies get messy, so skip for now
    continue if quote.parentNode.className is \smaller

    id = quote.hash.substring 2
    if (post = Post[id])
      if post.comment.length > 0
        document.createElement \div
          # <br>s don't show in textContent for some reason
          &innerHTML = post.comment.replace /<br>/g ' '

          for &querySelectorAll \.quotelink => &remove!
          # manually splitting spoiler elements is tiring, so just remove
          for &querySelectorAll \s => &remove!

          text = &textContent
        quote.after with document.createElement \span
          &textContent = truncate text, 70
          &className = \forcedquote

      if post.image
        quote.after with document.createElement \a
          &className = \forcedimage
          &textContent = ' '
          &setAttribute \data-width post.image.width
          &setAttribute \data-height post.image.height
          &href = post.image.url
          &appendChild with document.createElement \img
            &className = \thumb
            &style <<<
              max-height: \15px
              display: \inline-block
              vertical-align: \middle

            &src = post.image.thumb.url

            # XXX I'm cheating ....
            &addEventListener \mouseover handle-preview

      # compact post number
      quote.textContent = "Â»#{post.idx}"
      quote.classList.add \forcequoted

if board.isThread
  onready !->
    console.time "munging quotelinks..."
    munge document
    console.timeEnd "munging quotelinks..."

  onpostinsert !->
    munge it.detail.post

