# add quote text to >> replies to be able to identify them without hover

{onpostinsert} = require \../utils/features
{L} = require \../utils/dom
Post = require \../classes/Post
{truncate} = require \../utils/string

# XXX cheating
{handle-preview} = require \./image-previews

munge = !(ctx) ->
  for quote of ctx.querySelectorAll '.quotelink:not(.backlink):not(.forcequoted)'
    # these capcodeReplies get messy, so skip for now
    continue if quote.parentNode.className is \smaller

    no = quote.hash.substring 2
    if (post = Post[no])
      if post.comment.length > 0
        L \div
          # <br>s don't show in textContent for some reason
          &innerHTML = post.comment.replace /<br>/g ' '

          for &querySelectorAll \.quotelink => &remove!
          # manually splitting spoiler elements is tiring, so just remove
          for &querySelectorAll \s => &remove!

          text = &textContent
        quote.after with L \span
          # trim beginning whitespace
          &textContent = ' ' + truncate text, 70 .replace /^\s+/ ''
          &className = 'quote forcedquote'

      if post.image
        quote.after with L \a
          &className = \forcedimage
          &textContent = ' '
          &setAttribute \data-width post.image.width
          &setAttribute \data-height post.image.height
          &href = post.image.url
          &appendChild with L \img
            &className = \thumb
            &style <<<
              max-height: \15px
              display: \inline-block
              vertical-align: \middle

            &src = post.image.thumb.url

            # XXX I'm cheating ....
            &addEventListener \mouseover handle-preview

      # compact post number
      quote.textContent = "Â»#{post.idx}"
      quote.classList.add \forcequoted

if board.isThread
  onpostinsert !-> munge it.detail.post

