# add quote text to >> replies to be able to identify them without hover

{onpostinsert, onready, onprerender} = require \../utils/features
{L} = require \../utils/dom
{truncate} = require \../utils/string

lightbox = require(\../utils/lightbox) -> it.dataset{src, width, height}

munge = !(ctx) ->
  for quote of ctx.querySelectorAll '.quotelink:not(.backlink):not(.forcequoted)'
    # these capcodeReplies get messy, so skip for now
    continue if quote.parentNode.className is \smaller

    no = quote.hash.substring 2
    if (post = board.posts[no])
      if post.comment.length > 0
        L \div
          # <br>s don't show in textContent for some reason
          &innerHTML = post.comment.replace /<br>/g ' '

          for &querySelectorAll \.quotelink => &remove!
          # manually splitting spoiler elements is tiring, so just remove
          for &querySelectorAll \s => &remove!

          text = &textContent
        quote.after with L \span
          # trim beginning whitespace
          &textContent = ' ' + truncate text, 70 .replace /^\s+/ ''
          &className = 'quote forcedquote'

      if post.image
        quote.after with L \img
          &className = \forcedimage
          &setAttribute \data-width  post.image.width
          &setAttribute \data-height post.image.height
          &setAttribute \data-src    post.image.url
          &style <<<
            max-height: \15px
            display: \inline-block
            vertical-align: \middle

          &src = if post.image.spoiler
            board.spoiler-url
          else
            post.image.thumb.url

          &addEventListener \mouseover lightbox

      # compact post number
      quote.textContent = "#{post.idx}"
      quote.classList.add \forcequoted

if board.isThread
  onprerender !->
    for @body.querySelectorAll \.post
      munge &
  # after real render, munge any new posts
  onready !->
    onpostinsert !-> munge @post

