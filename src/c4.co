# c4, main code entry point

# XXX most of these `require`s calls just run side effects, due to legacy code
# structure. Should rethink flow

console.group "c4"

console.timeStamp "c4-init"
console.time "init"
console.time "interactive"

# `board` holds global state about the current page, like `document` for the
# 4chan domain.
global.board = {}
  # boards.4chan.org/<name>/'thread' or <page>/<threadid>, or catalog/<searchterm>
  [ , &name, page, &thread-no] = window.location.pathname / \/

  &is-catalog = page is \catalog
  &search-term = &thread-no if &is-catalog

  &is-thread   = not &is-catalog and !!&thread-no
  &is-board    = not &is-catalog and not &is-thread
  &page        = parseInt page, 10 or 0

  &url         = "//boards.4chan.org/#{&name}/"

  &ready       = false # flag similar to document.readyState

  &favicons = require \./favicon-data

  # TODO moot's crazy spoiler number bullshit
  &spoiler-url = "//s.4cdn.org/image/spoiler-#{&name}1.png"
  &thumbs-base = "//t.4cdn.org/#{&name}/"
  &images-base = "//i.4cdn.org/#{&name}/"

require \./archives
require \./backlinks
require \./poster
require \./updater
require \./onready

# features
# TODO why `forcequotes` is a feature and `enhancer` isn't is kind of
# arbitrary. Should think more about naming and directory structure.
#
# Would be nicer if features could register themselves without explicit `require`,
# but such is the nature of node modules as opposed to concatenation, I guess.
require \./features/forcequotes
require \./features/hide-message
require \./features/highlight
require \./features/image-expansion
require \./features/image-previews
require \./features/inlinereplies
require \./features/postpreviews
require \./features/quote
require \./utils/relative-dates
require \./features/relative-dates
require \./features/youtube

{onready} = require \./utils/features

onready !->
  console.timeEnd "onready handlers"
  console.timeEnd "interactive"
  console.timeStamp "c4-loaded"
  console.groupEnd!

console.timeEnd "init"
