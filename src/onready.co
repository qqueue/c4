<-! document.addEventListener \DOMContentLoaded

# sys.4chan.org is where the post happens
# so to fake a non-reloading ajax
if window.location.hostname is \sys.4chan.org
	log "sys.4chan listener"
	# pass message back to host page
	window.parent.postMessage document.body.innerHTML, \http://boards.4chan.org

	# switch to about:blank to prevent meta redirect
	# on 'post successful' from loading the board page
	# inside the hidden iframe
	window.location = 'about:blank'

	return # stop processing the rest of the script

board.name = window.location.pathname.split \/ .1

board.archive = switch board.name
	case <[a jp m tg u tv v vg]> # foolz
		"http://archive.foolz.us/#that/thread"
	case \lit
		"http://fuuka.warosu.org/#that/thread"
	case <[diy g sci]>
		"http://archive.installgentoo.net/#that/thread"
	case <[3 adv an ck co fa fit int k mu n o p po pol r9k soc sp toy trv vp x]>
		"http://archive.no-ip.org/#that/thread"

# used in enhancer
board.archivelink = if board.archive
	"""$1<a href="#{board.archive}/$2" class="archivelink">&gt;&gt;$2</a>"""
else \$& # don't replace

# archive redirection
if /404/.test document.title and board.archive
	if /\d+/.exec window.location.pathname
		window.location = "#{board.archive}/#{that.0}"
	return

# get other useful information
with board
	logoEl = document.getElementsByClassName \logo .0
	centerEls = document.getElementsByTagName \center

	@title = logoEl.children.2.children.0.children.0.textContent
	@subtitle = logoEl.children.4?innerHTML

	# detect based on the yellow theme
	@nsfw = document.styleSheets.0.ownerNode.href is \http://static.4chan.org/css/yotsuba.9.css
	@nav = document.getElementById \navtop .innerHTML # I could hard code it, but then I'd miss updates
	@banner = document.getElementsByTagName \img .0.src
	
	# the captcha, 2 ads, and footer tag are also in center tags
	@motd = centerEls.2.innerHTML if centerEls.length > 4
	@url = "http://boards.4chan.org/#{@name}/"
	@threadurl = "#{@url}res/"
	
	if document.location.pathname.match /res\/(\d+)/
		@isThread = true
		@threadId = that.1
		@threadPath = "/#{@name}/res/#{@threadId}"
	else
		@isThread = false
		@page = parseInt document.location.pathname.split('/')?2, 10 or 0
		@pages = for i to 15 # let's assume they all have 15 pages
			num: i, current: i is @page
		@next = if @current < 15 then @current + 1
		@previous = if @current > 0 then @current - 1
	@isBoard = !@isThread
	@locked = document.forms.length is 1 # delform, without postform


# disable 4chan styles
remove el for el of document.querySelectorAll 'link[rel*="stylesheet"], style'
# disable 4chan's onload functions
# we can do this because onload has to wait
# for all the images, which obstensibly takes longer
# than it does to load this script
window.onload = window.onunload = undefined

time "parse"
data = parser.yotsuba document
timeEnd "parse"

time "render"
with document.createElement \body
	@id = board.name
	@classList.add if board.nsfw then \nsfw else \sfw
	@classList.add if board.isThread then \threadpage else \boardpage
	time "parse innerHTML"
	@innerHTML = 
		"""
		%board.co.html%
		"""
	timeEnd "parse innerHTML"
	time "replace body"
	document.documentElement.replaceChild this, document.body
	timeEnd "replace body"

document.head.appendChild do
	create \style ->
		@id = \html5chan-style
		@textContent = 
			'''
			%hakase.css%
			'''

# add baord pages
# man, pagination logic is stupidly complicated
# also TODO report that missing parenthesis bug
# TODO inline this into main rendering to avoid
# rerender
if board.isBoard then do ->
	pages = kup \ul ->
		if ( page = board.page ) > 0
			@li -> @a {href: page - 1} \previous
		for page from 0 to 15
			@li ->
				# page board/0 redirects to board/ anyway
				@a {href: page or board.url} page
		if ( page = board.page ) < 15
			@li -> @a {href: page + 1} \next
	
	pages.children[if board.page then that + 1 else 0].firstChild.id = \current
	document.getElementById \pages .appendChild pages

# create or restore delete password 
# TODO inline this to avoid rerendering
do ->
	password = get \password or Math.random!.toString!.substr -8
	document.getElementById \delpassword .value = password
	document.getElementById \delpassword .title = password
	if field = document.getElementById \password
		field.value = password
		field.title = password
		save = -> set password: field.value
		field.addEventListener \input save # allows saving of custom passwords
		document.getElementById \postform .addEventListener \submit save # also save to localStorage on submit

# create recaptcha with script already included on page (using 4chan's public key)
document.head.appendChild do
	create \script ->
		@textContent =
			"if( document.getElementById('captcha') ) {
				Recaptcha.create('6Ldp2bsSAAAAAAJ5uyx_lx34lJeEpTLVkP5k04qc', 'captcha', {
						theme: 'clean',
						tabindex: 10,
					});
			}"

# rescroll to target element if this page hasn't been scrolled before
# this retains the browser's natural scroll position memory
# while still scrolling to the new hash target's position 
# the first time the page loads (or if window hasn't been scrolled
if window.location.hash and not sget document.URL 
	window.location.hash = window.location.hash
	window.addEventListener \scroll function registerPage
		sset (document.URL): true
		window.removeEventListener \scroll registerPage
	
timeEnd "render"

# mess with title
# TODO add number of posts
if board.isThread
	with data.thread.op
		if @title
			title = truncate that
		else
			title = ''
			title += "#{@poster} - " if @poster is not \Anonymous
			if (op = document.getElementById(@id).querySelector('.comment').textContent).length > 0
				title += truncate op
			else if @image
				title += truncate that.filename
			else
				title = @time.prettyPrint!
		title += " - /#{board.name}/" 
		titler.text = title


# autoupdate threads
if board.isThread
	updater.countdown!
	listen document.getElementById \update-now .on \click ->
		updater.update!
else
	document.getElementById \updater .hidden = true
# enable all features
# TODO load settings and selectively load features
for key, feature in features
	feature.on! unless key is \postpreview

do ->
	listen document.getElementById \dump-log .on \click ->
		console.log log.messages

# run other onready handlers
onready.execute!
