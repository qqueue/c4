<-! document.addEventListener \DOMContentLoaded
# TODO handle archive redirection here

with board
	logoEl = document.getElementsByClassName \logo .0
	centerEls = document.getElementsByTagName \center

	@name = document.title.match /\/(\w+)\// .1 # easiest way to get it 
	@title = logoEl.children.2.children.0.children.0.textContent
	@subtitle = logoEl.children.4?innerHTML

	# detect based on the yellow theme
	@nsfw = document.styleSheets.0.ownerNode.href is \http://static.4chan.org/css/yotsuba.9.css
	@nav = document.getElementById \navtop .innerHTML # I could hard code it, but then I'd miss updates
	@banner = document.getElementsByTagName \img .0.src
	
	# the captcha, 2 ads, and footer tag are also in center tags
	@motd = centerEls.2.innerHTML if centerEls.length > 4
	@url = "http://boards.4chan.org/#{@name}/"
	@threadurl = "#{@url}res/"
	
	if document.location.pathname.match /res\/(\d+)/
		@isThread = true
		@threadId = that.1
		@threadPath = "/#{@name}/res/#{@threadId}"
	else
		@isThread = false
		@page = parseInt document.location.pathname.split('/')?2, 10 or 0
		@pages = for i to 15 # let's assume they all have 15 pages
			num: i, current: i is @page
		@next = if @current < 15 then @current + 1
		@previous = if @current > 0 then @current - 1
	@isBoard = !@isThread
	@locked = document.forms.length is 1 # delform, without postform

# FIXME 
# this should go somewhere better
archivelink = (url) -> """$1<a href="#url/$2" class="archivelink">&gt;&gt;$2</a>"""
board.archive = switch board.name
	case \a \jp \m \tg \u \tv \v \vg # foolz
		archivelink "http://archive.foolz.us/#that/thread"
	case \lit
		archivelink "http://fuuka.warosu.org/#that/thread"
	case \diy \g \sci
		archivelink "http://archive.installgentoo.net/#that/thread"
	case \3 \adv \an \ck \co \fa \fit \int \k \mu \n \o \p \po \pol \r9k \soc \sp \toy \trv \vp \x
		archivelink "http://archive.no-ip.org/#that/thread"
	default "$&" # don't replace

# disable 4chan styles
remove el for el of document.querySelectorAll 'link[rel*="stylesheet"], style'
# disable 4chan's onload functions
# we can do this because onload has to wait
# for all the images, which obstensibly takes longer
# than it does to load this script
window.onload = window.onunload = undefined

time "parse"
data = parser.yotsuba document
timeEnd "parse"

time "render"
# TODO eventually this should replaced with a less innerHTML-based re-rendering
# solution, so the attributes won't have to be removed
# and firefox apparently won't have to spend time clearing out
# the old elements
with document.createElement \body
	@id = board.name
	@classList.add if board.nsfw then \nsfw else \sfw
	@classList.add if board.isThread then \threadpage else \boardpage
	time "parse innerHTML"
	@innerHTML = 
		"""
		%board.co.html%
		"""
	timeEnd "parse innerHTML"
	time "replace body"
	document.documentElement.replaceChild this, document.body
	timeEnd "replace body"

document.head.appendChild do
	create \style ->
		@id = \html5chan-style
		@textContent = 
			'''
			%hakase.css%
			'''

# add baord pages
# man, pagination logic is stupidly complicated
# also TODO report that missing parenthesis bug
if board.isBoard then do ->
	pages = kup \ul ->
		if ( page = board.page ) > 0
			@li -> @a {href: page - 1} \previous
		for page from 0 to 15
			@li ->
				# page board/0 redirects to board/ anyway
				@a {href: page or board.url} page
		if ( page = board.page ) < 15
			@li -> @a {href: page + 1} \next
	
	pages.children[if board.page then that + 1 else 0].firstChild.id = \current
	document.getElementById \pages .appendChild pages

# create or restore delete password 
do ->
	password = get \password or Math.random!.toString!.substr -8
	document.getElementById \delpassword .value = password
	document.getElementById \delpassword .title = password
	if field = document.getElementById \password
		field.value = password
		field.title = password
		save = -> set password: field.value
		field.addEventListener \input save # allows saving of custom passwords
		document.getElementById \postform .addEventListener \submit save # also save to localStorage on submit

# create recaptcha with script already included on page (using 4chan's public key)
document.head.appendChild do
	create \script ->
		@textContent =
			"if( document.getElementById('captcha') ) {
				Recaptcha.create('6Ldp2bsSAAAAAAJ5uyx_lx34lJeEpTLVkP5k04qc', 'captcha', {
						theme: 'clean',
						tabindex: 10,
					});
			}"

# rescroll to target element if this page hasn't been scrolled before
# this retains the browser's natural scroll position memory
# while still scrolling to the new hash target's position 
# the first time the page loads (or if window hasn't been scrolled
if window.location.hash and not sget document.URL 
	window.location.hash = window.location.hash
	window.addEventListener 'scroll', function registerPage
		sset (document.URL): true
		window.removeEventListener 'scroll', registerPage
	
timeEnd "render"

# mess with title
# TODO add number of posts
if board.isThread
	with data.thread.op
		truncate = -> if it.length > 20 then "#{it.substring 0 20}..." else it
		if @title
			document.title = truncate that
		else
			document.title = ''
			document.title += "#that - " if @poster is not \Anonymous
			if (op = document.getElementById(@id).querySelector('.comment').textContent).length > 0
				document.title += truncate op
			else if @image
				document.title += truncate that.filename
			else
				document.title = @time.prettyPrint!
		document.title += " - /#{board.name}/" 


# autoupdate threads
if board.isThread
	updater.countdown!

# enable all features
# TODO load settings and selectively load features
for key, feature in features
	feature.on! unless key is \postpreview

